//bintray plugin
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = PROJ_GROUP

// Configure android-maven-gradle-plugin
install.repositories.mavenInstaller.pom.project {

    name PROJ_NAME
    description PROJ_NAME
    packaging POM_PACKAGING
    url POM_URL

    licenses {
        license {
            name POM_LICENCE_NAME
            url POM_LICENCE_URL
            distribution POM_LICENCE_DIST
        }
    }

    scm {
        url POM_SCM_URL
        connection POM_SCM_CONNECTION
        developerConnection POM_SCM_DEV_CONNECTION
    }

    developers {
        developer {
            id DEVELOPER_ID              // your user id (you can write your nickname)
            name DEVELOPER_NAME          // your user name
            email DEVELOPER_EMAIL        // your email
        }
    }

}

if (project.plugins.hasPlugin('com.android.library')) {
    android.libraryVariants.all { variant ->
        Task javadocTask = task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
            failOnError = false
            group = 'artifact'
            description "Generates Javadoc for $variant.name"

            // Source files from the variant
            source = variant.javaCompiler.source

            // Classpath from the variant + android.jar
            String androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
            classpath = variant.javaCompiler.classpath + files(androidJar)

            // The Android online reference doesn't include package-list, so we have to use the local one
            String packageListRef = "${android.sdkDirectory}/docs/reference/"
            options.linksOffline 'http://d.android.com/reference/', packageListRef

            options.encoding 'UTF-8'
            options.charSet 'UTF-8'

            // Exclude generated files
            exclude '**/BuildConfig.java'
            exclude '**/R.java'

            // Output to a unique javadoc folder per variant
            destinationDir = new File(project.docsDir, "javadoc-$variant.name")

            if (JavaVersion.current().isJava8Compatible()) {
                options.addStringOption('Xdoclint:none', '-quiet')
            }
        }

        // For official releasese, don't prefix the name so the artifact is published correctly
        // (Can't seem to modify it for publishing, for whatever reason...)
        String classifierPrefix = (variant.name == 'release') ? '' : "$variant.name-"

        Task javadocJarTask = task("generate${variant.name.capitalize()}JavadocJar", type: Jar, dependsOn: javadocTask) {
            group = 'artifact'
            description = "Generates Javadoc jar for $variant.name"

            classifier = "${classifierPrefix}javadoc"
            from javadocTask.destinationDir
        }

        Task sourcesJarTask = task("generate${variant.name.capitalize()}SourcesJar", type: Jar) {
            group = 'artifact'
            description = "Generates sources jar for $variant.name"

            classifier = "${classifierPrefix}sources"
            from variant.javaCompiler.source
        }

        if (variant.name == 'release') {
            // There's a lot of "magic" around the archives configuration; easier
            // just to embrace it rather than try to configure around it
            artifacts {
                archives javadocJarTask, sourcesJarTask
            }
        }
        else {
            // Create a configuration we can publish from for each variant
            String configurationName = "archives${variant.name.capitalize()}"
            configurations.create(configurationName)
            artifacts.add configurationName, javadocJarTask
            artifacts.add configurationName, sourcesJarTask
        }
    }
}

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {
    bintray.user = project.bintrayUser
    bintray.key = project.bintrayKey
}

// Configure gradle-bintray-plugin (for publishing releases)
bintray {
    configurations = ['archives']

    publish = true

    pkg {
        repo = 'maven'
        name = PROJ_NAME
        licenses = ['Apache-2.0']
        websiteUrl = POM_URL
        issueTrackerUrl = POM_ISSUE_URL
        vcsUrl = POM_VCS_URL
    }
}